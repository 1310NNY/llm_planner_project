(define (domain barman)
  (:requirements :strips :typing)
  (:types hand level beverage dispenser container - object
  	  ingredient cocktail - beverage
          shot shaker - container)
  (:predicates (next ?l1 ?l2 - level)
                (dispenses ?d - dispenser ?i - ingredient)
                (cocktail-part1 ?c - cocktail ?i - ingredient)
                (cocktail-part2 ?c - cocktail ?i - ingredient)
                (ontable ?c - container)
                (handempty ?h - hand)
                (holding ?h - hand ?c - container)
                (empty ?c - container)
                (clean ?c - container)
                (contains ?c - container ?b - beverage)
                (used ?c - container ?b - beverage)
                (shaker-empty-level ?s - shaker ?l - level)
                (shaker-level ?s - shaker ?l - level)
                (unshaked ?s - shaker)
                (shaked ?s - shaker))
		
  (:action grasp
             :parameters (?h - hand ?c - container)
             :precondition (and (ontable ?c) (handempty ?h))
             :effect (and (holding ?h ?c)
                          (not (ontable ?c))
                          (not (handempty ?h))))

  (:action leave
             :parameters (?h - hand ?c - container)
             :precondition (holding ?h ?c)
             :effect (and (ontable ?c)
                          (handempty ?h)
                          (not (holding ?h ?c))))
  
  (:action fill-shot
           :parameters (?s - shot ?i - ingredient ?h1 ?h2 - hand ?d - dispenser)
           :precondition (and (holding ?h1 ?s)
                              (handempty ?h2)
                              (dispenses ?d ?i)
                              (empty ?s)
                              (clean ?s))
           :effect (and (contains ?s ?i)
                        (used ?s ?i)
                        (not (empty ?s))
                        (not (clean ?s))))

  (:action refill-shot
           :parameters (?s - shot ?i - ingredient ?h1 ?h2 - hand ?d - dispenser)
           :precondition (and (holding ?h1 ?s)
                              (handempty ?h2)
                              (dispenses ?d ?i)
                              (empty ?s)
                              (used ?s ?i))
           :effect (and (contains ?s ?i)
                        (not (empty ?s))))

  (:action pour-shot-to-clean-shaker
           :parameters (?s - shot ?i - ingredient ?d - shaker ?h1 - hand ?l ?l1 - level)
           :precondition (and (holding ?h1 ?s)
                              (contains ?s ?i)
                              (empty ?d)
                              (clean ?d)
                              (shaker-level ?d ?l)
                              (next ?l ?l1))
           :effect (and (contains ?d ?i)
                        (unshaked ?d)
                        (shaker-level ?d ?l1)
                        (empty ?s)
                        (not (contains ?s ?i))
                        (not (empty ?d))
                        (not (clean ?d))
                        (not (shaker-level ?d ?l))))

  (:action pour-shot-to-used-shaker
           :parameters (?s - shot ?i - ingredient ?d - shaker ?h1 - hand ?l ?l1 - level)
           :precondition (and (holding ?h1 ?s)
                              (contains ?s ?i)
                              (unshaked ?d)
                              (shaker-level ?d ?l)
                              (next ?l ?l1))
           :effect (and (contains ?d ?i)
                        (shaker-level ?d ?l1)
                        (empty ?s)
                        (not (contains ?s ?i