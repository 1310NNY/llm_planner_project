(define (domain thoughtful-typed)
	(:requirements :typing)
	(:types card colnum num suittype)
	(:predicates
		(colspace ?n - colnum)
		(colsuccessor ?n1 - colnum ?n0 - colnum)
		(successor ?n1 - num ?n0 - num)
		(on ?c1 - card ?c2 - card)
		(ace ?c - card)
		(king ?c - card)
		(clear ?c - card)
		(bottomtalon ?c - card)
		(toptalon ?c - card)
		(ontalon ?c1 - card ?c2 - card)
		(talonplayable ?c - card)
		(instack ?c - card)
		(home ?c - card)
		(faceup ?c - card)
		(bottomcol ?c - card)
		(suit ?c - card ?s - suittype)
		(value ?c - card ?v - num)
		(canstack ?c1 - card ?c2 - card)
	)

	(:action move-col-to-col
		:parameters (?card ?oldcard ?newcard - card)
		:precondition (and 
			(on ?card ?oldcard)
			(faceup ?card)
			(clear ?newcard)
			(canstack ?card ?newcard))
		:effect (and
			(on ?card ?newcard)
			(clear ?oldcard)
			(faceup ?oldcard)
			(not (on ?card ?oldcard))
			(not (clear ?newcard))))

	(:action move-col-to-col-b
		:parameters (?card ?newcard - card ?cols ?ncols - colnum)
		:precondition (and 
			(bottomcol ?card)
			(faceup ?card)
			(clear ?newcard)
			(canstack ?card ?newcard)
			(colspace ?cols)
			(colsuccessor ?ncols ?cols))
		:effect (and 
			(on ?card ?newcard)
			(colspace ?ncols)
			(not (bottomcol ?card))
			(not (clear ?newcard))
			(not (colspace ?cols))))

	(:action move-col-to-col-c
		:parameters (?card ?oldcard - card ?cols ?ncols - colnum)
		:precondition (and
			(on ?card ?oldcard)
			(faceup ?card)
			(king ?card)
			(colspace ?cols)
			(colsuccessor ?cols ?ncols))
		:effect (and
			(bottomcol ?card)
			(clear ?oldcard)
			(faceup ?oldcard)
			(colspace ?ncols)
			(not (on ?card ?oldcard))
			(not (colspace ?cols))))

	(:action col-to-home
		:parameters (?card ?oldcard - card ?suit - suittype ?vcard - num
				?homecard - card ?vhomecard - num)
		:precondition (and
			(on ?card ?oldcard)
			(clear ?card)
			(home ?homecard)
			(suit ?card ?suit)
			(suit ?homecard ?suit)
			(value ?card ?vcard)
			(value ?homecard ?vhomecard)
			(successor ?vcard ?vhomecard))
		:effect (and
			(home ?card)
			(clear ?oldcard)
			(faceup ?oldcard)
			(on ?card ?homecard)
			(not (on ?card ?oldcard))
			(not (home ?homecard))
			(not (faceup ?card))
			(not (clear ?card))))

	(:action col-to-home-b
		:parameters (?card ?homecard - card ?suit - suittype ?vcard - num 
			?vhomecard - num ?cols ?ncols - colnum)
		:precondition (and
			(bottomcol ?card)
			(clear ?card)
			(home ?homecard)
			(suit ?card ?suit)
			(suit ?homecard ?suit)
			(value ?card ?vcard)
			(value ?homecard ?vhomecard)
			(successor ?vcard ?vhomecard)
			(colspace ?cols)
			(colsuccessor ?ncols ?cols))
		:effect (and
			(home ?card)
			(colspace ?ncols)
			(not (home ?homecard))
			(not (faceup ?card))
			(not (clear ?card))
			(not (bottomcol ?card))
			(not (colspace ?cols))))

	(:action tal-to-col
		:parameters (?card ?oldcard ?newcard ?cardabove - card)
		:precondition (and
			(ontalon ?card ?oldcard)
			(ontalon ?cardabove ?card)
			(clear ?newcard)
			(talonplayable ?card)
			(canstack ?card ?newcard))
		:effect (and
			(on ?card ?newcard)
			(clear ?card)
			(faceup ?card)
			(talonplayable ?oldcard)
			(ontalon ?cardabove ?oldcard)
			(not (clear ?newcard))
			(not (talonplayable ?card))
			(not (ontalon ?card ?oldcard))
			(not (ontalon ?cardabove ?card))))

	(:action tal-to-col-b
		:parameters (?card ?newcard ?cardabove - card)
		:precondition (and
			(ontalon ?cardabove ?card)
			(clear ?newcard)
			(talonplayable ?card)
			(bottomtalon ?card)
			(canstack ?card ?newcard))
		:effect (and
			(on ?card ?newcard)
			(clear ?card)
			(faceup ?card)
			(bottomtalon ?cardabove)
			(talonplayable ?cardabove)
			(not (bottomtalon ?card))
			(not (talonplayable ?card))
			(not (clear ?newcard))
			(not (ontalon ?cardabove ?card))))

	(:action tal-to-col-c
		:parameters (?card ?newcard ?oldcard - card)
		:precondition (and
			(ontalon ?card ?oldcard)
			(clear ?newcard)
			(canstack ?card ?newcard)
			(talonplayable ?card)
			(toptalon ?card))
		:effect (and
			(on ?card ?newcard)
			(clear ?card)
			(faceup ?card)
			(toptalon ?oldcard)
			(talonplayable ?oldcard)
			(not (clear ?newcard))
			(not (toptalon ?card))
			(not (talonplayable ?card))
			(not (ontalon ?card ?oldcard))))

	(:action tal-to-col-d
		:parameters (?card ?newcard - card)
		:precondition (and
			(clear ?newcard)
			(canstack ?card ?newcard)
			(bottomtalon ?card)
			(toptalon ?card)
			(talonplayable ?card))
		:effect (and
			(on ?card ?newcard)
			(clear ?card)
			(faceup ?card)
			(not (clear ?newcard))
			(not (toptalon ?card))
			(not (talonplayable ?card))
			(not (bottomtalon ?card))))

	(:action tal-to-col-e
		:parameters (?card ?oldcard ?cardabove - card ?cols ?ncols - colnum)
		:precondition (and
			(ontalon ?card ?oldcard)
			(ontalon ?cardabove ?card)
			(king ?card)
			(talonplayable ?card)
			(colspace ?cols)
			(colsuccessor ?cols ?ncols))
		:effect (and
			(bottomcol ?card)
			(clear ?card)
			(faceup ?card)
			(talonplayable ?oldcard)
			(ontalon ?cardabove ?oldcard)
			(colspace ?ncols)
			(not (colspace ?cols))
			(not (talonplayable ?card))
			(not (ontalon ?card ?oldcard))
			(not (ontalon ?cardabove ?card))))

	(:action tal-to-col-f
		:parameters (?card ?cardabove - card ?cols ?ncols - colnum)
		:precondition (and
			(ontalon ?cardabove ?card)
			(king ?card)
			(talonplayable ?card)
			(bottomtalon ?card)
			(colspace ?cols)
			(colsuccessor ?cols ?ncols))
		:effect (and
			(bottomcol ?card)
			(clear ?card)
			(faceup ?card)
			(bottomtalon ?cardabove)
			(talonplayable ?cardabove)
			(colspace ?ncols)
			(not (colspace ?cols))
			(not (bottomtalon ?card))
			(not (talonplayable ?card))
			(not (ontalon ?cardabove ?card))))

	(:action tal-to-col-g
		:parameters (?card ?oldcard - card ?cols ?ncols - colnum)
		:precondition (and
			(ontalon ?card ?oldcard)
			(king ?card)
			(talonplayable ?card)
			(colspace ?cols)
			(colsuccessor ?cols ?ncols)
			(toptalon ?card))
		:effect (and
			(bottomcol ?card)
			(clear ?card)
			(faceup ?card)
			(toptalon ?oldcard)
			(talonplayable ?oldcard)
			(colspace ?ncols)
			(not (colspace ?cols))
			(not (toptalon ?card))
			(not (talonplayable ?card))
			(not (ontalon ?card ?oldcard))))

	(:action tal-to-col-h
		:parameters (?card - card ?cols ?ncols - colnum)
		:precondition (and
			(king ?card)
			(bottomtalon ?card)
			(toptalon ?card)
			(colspace ?cols)
			(colsuccessor ?cols ?ncols)
			(talonplayable ?card))
		:effect (and
			(bottomcol ?card)
			(clear ?card)
			(faceup ?card)
			(colspace ?ncols)
			(not (colspace ?cols))
			(not (toptalon ?card))
			(not (talonplayable ?card))
			(not (bottomtalon ?card))))

	(:action tal-to-home
		:parameters (?card ?cardabove ?cardbelow ?homecard - card ?cardsuit - suittype
			?vcard ?vhomecard - num)
		:precondition (and
			(ontalon ?cardabove ?card)
			(ontalon ?card ?cardbelow)
			(talonplayable ?card)
			(suit ?card ?cardsuit)
			(suit ?homecard ?cardsuit)
			(home ?homecard)
			(value ?card ?vcard)
			(value ?homecard ?vhomecard)
			(successor ?vcard ?vhomecard))
		:effect (and
			(home ?card)
			(talonplayable ?cardbelow)
			(ontalon ?cardabove ?cardbelow)
			(not (home ?homecard))
			(not (talonplayable ?card))
			(not (ontalon ?cardabove ?card))
			(not (ontalon ?card ?cardbelow))))

	(:action tal-to-home-b
		:parameters (?card ?cardabove ?homecard  - card ?cardsuit - suittype
			?vcard ?vhomecard - num)
		:precondition (and
			(ontalon ?cardabove ?card)
			(talonplayable ?card)
			(bottomtalon ?card)
			(home ?homecard)
			(suit ?card ?cardsuit)
			(suit ?homecard ?cardsuit)
			(value ?card ?vcard)
			(value ?homecard ?vhomecard)
			(successor ?vcard ?vhomecard))
		:effect (and
			(home ?card)
			(bottomtalon ?cardabove)
			(talonplayable ?cardabove)
			(not (home ?homecard))
			(not (bottomtalon ?card))
			(not (talonplayable ?card))
			(not (ontalon ?cardabove ?card))))

	(:action tal-to-home-c
		:parameters (?card ?cardbelow ?homecard - card ?cardsuit - suittype
			?vcard ?vhomecard - num)
		:precondition (and
			(ontalon ?card ?cardbelow)
			(talonplayable ?card)
			(toptalon ?card)
			(home ?homecard)
			(suit ?card ?cardsuit)
			(suit ?homecard ?cardsuit)
			(value ?card ?vcard)
			(value ?homecard ?vhomecard)
			(successor ?vcard ?vhomecard))
		:effect (and
			(home ?card)
			(toptalon ?cardbelow)
			(talonplayable ?cardbelow)
			(not (home ?homecard))
			(not (toptalon ?card))
			(not (talonplayable ?card))
			(not (ontalon ?card ?cardbelow))))

	(:action tal-to-home-d
		:parameters (?card ?homecard - card ?cardsuit - suittype
			?vcard ?vhomecard - num)
		:precondition (and
			(bottomtalon ?card)
			(toptalon ?card)
			(talonplayable ?card)
			(home ?homecard)
			(suit ?card ?cardsuit)
			(suit ?homecard ?cardsuit)
			(value ?card ?vcard)
			(value ?homecard ?vhomecard)
			(successor ?vcard ?vhomecard))
		:effect (and
			(home ?card)
			(not (home ?homecard))
			(not (toptalon ?card))
			(not (talonplayable ?card))
			(not (bottomtalon ?card))))

	(:action home-to-col
		:parameters (?card ?cardbelow ?newcard - card ?cardsuit - suittype
			?vcard ?vcardbelow - num)
		:precondition (and
			(home ?card)
			(suit ?card ?cardsuit)
			(suit ?cardbelow ?cardsuit)
			(value ?card ?vcard)
			(value ?cardbelow ?vcardbelow)
			(successor ?vcard ?vcardbelow)
			(canstack ?card ?newcard)
			(clear ?newcard))
		:effect (and
			(on ?card ?newcard)
			(clear ?card)
			(faceup ?card)
			(home ?cardbelow)
			(not (home ?card))
			(not (clear ?newcard))))

	(:action home-to-col-a
		:parameters (?card ?cardbelow - card ?cardsuit - suittype
			?vcard ?vcardbelow - num ?cols ?ncols - colnum)
		:precondition (and
			(home ?card)
			(king ?card)
			(suit ?card ?cardsuit)
			(suit ?cardbelow ?cardsuit)
			(value ?card ?vcard)
			(value ?cardbelow ?vcardbelow)
			(successor ?vcard ?vcardbelow)
			(colspace ?cols)
			(colsuccessor ?cols ?ncols))
		:effect (and
			(home ?cardbelow)
			(bottomcol ?card)
			(clear ?card)
			(faceup ?card)
			(colspace ?ncols)
			(not (colspace ?cols))
			(not (home ?card))))

	(:action turn-deck
		:parameters (?card ?c1 - card)
		:precondition (and
			(ontalon ?c1 ?card)
			(talonplayable ?card))
		:effect (and
			(talonplayable ?c1)
			(not (talonplayable ?card))))

	(:action turn-deck-a
		:parameters (?card ?c1 - card)
		:precondition (and
			(toptalon ?card)
			(talonplayable ?card)
			(bottomtalon ?c1))
		:effect (and
			(talonplayable ?c1)
			(not (talonplayable ?card)))))